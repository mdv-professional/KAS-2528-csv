PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vldossier: <https://data.vlaanderen.be/ns/dossier#>
PREFIX vlbesluitvorming: <http://data.vlaanderen.be/ns/besluitvorming#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

SELECT ?jaartal ?thema ?dossier_URI ?dossier_titel
WHERE {
  VALUES (?jaartal_query) { (2018) (2019) (2020) }

  GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
  {
      {
        SELECT ?dossier_URI ?nieuwsbriefInfo_URI (MAX(?nieuwsbriefInfo_datum_o) AS ?nieuwsbriefInfo_datum)
        WHERE {
          GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
            ?dossier_URI vldossier:doorloopt
              /^vlbesluitvorming:vindtPlaatsTijdens
              /vlbesluitvorming:genereertAgendapunt
              /^vlbesluitvorming:heeftOnderwerp
              /prov:generated ?nieuwsbriefInfo_URI .
          }

          OPTIONAL { ?nieuwsbriefInfo_URI dct:issued ?nieuwsbriefInfo_datum_v1 . }
          OPTIONAL { ?nieuwsbriefInfo_URI ext:aangepastOp ?nieuwsbriefInfo_datum_v2 . } 

          BIND (COALESCE(?nieuwsbriefInfo_datum_v1, ?nieuwsbriefInfo_datum_v2, "2000-01-01"^^xsd:dateTime) AS ?nieuwsbriefInfo_datum_o)
        }
        GROUP BY ?dossier_URI ?nieuwsbriefInfo_URI
      }
    }

    OPTIONAL { ?dossier_URI dct:title ?dossier_standaard_titel . }
    OPTIONAL { ?dossier_URI dct:alternative ?dossier_alternatieve_titel . }

    OPTIONAL { ?nieuwsbriefInfo_URI dct:title ?nieuwsbriefInfo_standaard_titel . }
    OPTIONAL { ?nieuwsbriefInfo_URI dct:alternative ?nieuwsbriefInfo_alternatieve_titel . }
  }

  OPTIONAL {
    { 
      SELECT DISTINCT *
      WHERE { 
         {
           GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
            { ?nieuwsbriefInfo_URI ext:themesOfSubcase ?themaCode . }
          } 
        } UNION {
          {
            GRAPH <http://mu.semte.ch/graphs/organizations/kanselarij> {
              { ?nieuwsbriefInfo_URI dct:subject ?themaCode . }
            }
          } 
        }
      }
    }

    GRAPH <http://mu.semte.ch/graphs/public> {
      ?themaCode skos:prefLabel ?thema_o .
    }
  }

  BIND (IF( BOUND(?thema_o), ?thema_o, "<Geen thema>") AS ?thema)

  BIND (YEAR(?nieuwsbriefInfo_datum) AS ?jaartal)
  FILTER (?jaartal = ?jaartal_query)

  BIND (
    IF(BOUND(?dossier_standaard_titel) && STRLEN(?dossier_standaard_titel) > 0, ?dossier_standaard_titel, 
    IF(BOUND(?dossier_alternatieve_titel) && STRLEN(?dossier_alternatieve_titel) > 0, ?dossier_alternatieve_titel, 
    "<Geen dossiertitel>")) AS ?dossier_titel)

  BIND (
    IF(BOUND(?nieuwsbriefInfo_standaard_titel) && STRLEN(?nieuwsbriefInfo_standaard_titel) > 0, ?dossier_standaard_titel, 
    IF(BOUND(?nieuwsbriefInfo_alternatieve_titel) && STRLEN(?nieuwsbriefInfo_alternatieve_titel) > 0, ?nieuwsbriefInfo_alternatieve_titel, 
    "<Geen nieuwsbrieftitel>")) AS ?nieuwsbriefInfo_titel)

}
ORDER BY ?jaartal ?thema ?nieuwsbriefInfo_datum